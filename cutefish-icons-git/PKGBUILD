# Maintainer: zyxxcwmj <zyxxcwmj@gmail.com>

pkgname=cutefish-icons-git
pkgver=0.5.r4.g4e47d89f
pkgrel=1
pkgdesc="System default icon theme of CutefishOS"
arch=('any')
url="https://github.com/cutefishos/icons"
license=('GPL')
groups=('cutefish-git')
depends=()
makedepends=('extra-cmake-modules' 'ninja')
provides=("cutefish-icons")
conflicts=("cutefish-icons")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd icons
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd icons
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd icons
  DESTDIR="$pkgdir" ninja install
}
