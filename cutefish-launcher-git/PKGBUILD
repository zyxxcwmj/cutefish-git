# Maintainer: zyxxcwmj <zyxxcwmj@gmail.com>

pkgname=cutefish-launcher-git
pkgver=0.5.r4.g7c3bf31
pkgrel=1
pkgdesc="CutefishOS's full-screen application launcher"
arch=('x86_64')
url="https://github.com/cutefishos/launcher"
license=('GPL')
groups=('cutefish-git')
depends=('fishui-git' 'kwindowsystem' 'libcutefish-git')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
provides=("cutefish-launcher")
conflicts=("cutefish-launcher")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd launcher
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd launcher
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd launcher
  make DESTDIR="$pkgdir" install
}
