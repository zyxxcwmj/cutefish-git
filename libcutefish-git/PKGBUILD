# Maintainer: zyxxcwmj <zyxxcwmj@gmail.com>

pkgname=libcutefish-git
pkgver=0.5.r2.gabac758
pkgrel=1
pkgdesc="System library for Cutefish applications"
arch=('x86_64')
url="https://github.com/cutefishos/libcutefish"
license=('GPL')
depends=('bluez-qt' 'kscreen' 'modemmanager-qt' 'networkmanager-qt' 'qt5-quickcontrols2'
         # via dbus:
         'accountsservice')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
provides=("libcutefish")
conflicts=("libcutefish")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd libcutefish
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd libcutefish
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd libcutefish
  DESTDIR="$pkgdir" ninja install
}
