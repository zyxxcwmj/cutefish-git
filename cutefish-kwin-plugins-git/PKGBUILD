# Maintainer: zyxxcwmj <zyxxcwmj@gmail.com>

pkgname=cutefish-kwin-plugins-git
pkgver=0.5.r1.gfd5f3e8
pkgrel=1
pkgdesc="CutefishOS KWin Plugins"
arch=('x86_64')
url="https://github.com/cutefishos/kwin-plugins"
license=('GPL')
groups=('cutefish-git')
depends=('kconfig' 'kdecoration' 'kguiaddons' 'kcoreaddons' 'kconfigwidgets' 'kwindowsystem'
         'kwayland' 'kwin'
         # qml:
         'fishui' 'kdeclarative' 'plasma-framework')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
provides=("cutefish-kwin-plugins")
conflicts=("deepin-kwin" "cutefish-kwin-plugins")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd kwin-plugins
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd kwin-plugins
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd kwin-plugins
  DESTDIR="$pkgdir" ninja install
}
