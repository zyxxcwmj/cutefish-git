# Maintainer: zyxxcwmj <zyxxcwmj@gmail.com>

pkgname=fishui-git
pkgver=0.5.r21.gd978beb
pkgrel=1
pkgdesc="GUI library based on QQC2 for Cutefish applications"
arch=('x86_64')
url="https://github.com/cutefishos/fishui"
license=('GPL')
depends=('kwindowsystem' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols2')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
provides=("fishui")
conflicts=("fishui")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd fishui
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd fishui
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd fishui
  DESTDIR="$pkgdir" ninja install
}
